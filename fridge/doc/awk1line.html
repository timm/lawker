<h1><join>Handy One-Liners For Awk (v0.22) </join></h1>
<p> Eric Pement <a href="mailto:pemente@northpark.edu">pemente@northpark.edu</a>      
<p>   Latest version of this file is usually at:
   <a href="http://www.student.northpark.edu/pemente/awk/awk1line.txt">
http://www.student.northpark.edu/pemente/awk/awk1line.txt</a>


<h2>USAGE</h2>
<pre>
Unix:     awk '/pattern/ {print "$1"}'    # standard Unix shells
DOS/Win:  awk '/pattern/ {print "$1"}'    # okay for DJGPP compiled
          awk "/pattern/ {print \"$1\"}"  # required for Mingw32
</pre>
<p>
Most of my experience comes from version of GNU awk (gawk) compiled for
Win32. Note in particular that DJGPP compilations permit the awk script
to follow Unix quoting syntax '/like/ {"this"}'. However, the user must
know that single quotes under DOS/Windows do not protect the redirection
arrows (&lt;, >) nor do they protect pipes (|). Both are special symbols
for the DOS/CMD command shell and their special meaning is ignored only
if they are placed within "double quotes." Likewise, DOS/Win users must
remember that the percent sign (%) is used to mark DOS/Win environment
variables, so it must be doubled (%%) to yield a single percent sign
visible to awk.
<p>
If I am sure that a script will NOT need to be quoted in Unix, DOS, or
CMD, then I normally omit the quote marks. If an example is peculiar to
GNU awk, the command 'gawk' will be used. Please notify me if you find
errors or new commands to add to this list (total length under 65
characters). I usually try to put the shortest script first.
<h2>File Spacing</h2>
<p>Double space a file
<pre>
 awk '1;{print ""}'
 awk 'BEGIN{ORS="\n\n"};1'
</pre>
<p>Double space a file which already has blank lines in it. Output file
should contain no more than one blank line between lines of text.
NOTE: On Unix systems, DOS lines which have only CRLF (\r\n) are
often treated as non-blank, and thus 'NF' alone will return TRUE.
<pre>
awk 'NF{print $0 "\n"}'
</pre>
<p>Triple space a file
<pre>awk '1;{print "\n"}'</pre>

<h2>Numbering and Calculations</h2>
 <p>Precede each line by its line number FOR THAT FILE (left alignment).
 Using a tab (\t) instead of space will preserve margins.
 <pre>awk '{print FNR "\t" $0}' files*</pre>
<p>Precede each line by its line number FOR ALL FILES TOGETHER, with tab.
<pre>awk '{print NR "\t" $0}' files*</pre>

 <p>Number each line of a file (number on left, right-aligned)
 Double the percent signs if typing from the DOS command prompt.
 <pre>awk '{printf("%5d : %s\n", NR,$0)}'</pre>

 <p>Number each line of file, but only print numbers if line is not blank
 Remember caveats about Unix treatment of \r (mentioned above)
 <pre>awk 'NF{$0=++a " :" $0};{print}'
 awk '{print (NF? ++a " :" :"") $0}'
</pre>
 <p> Count lines (emulates "wc -l")
 <pre>awk 'END{print NR}'</pre>

 <p> Print the sums of the fields of every line
 <pre>awk '{s=0; for (i=1; i<=NF; i++) s=s+$i; print s}'</pre>

 <p>Add all fields in all lines and print the sum
 <pre>awk '{for (i=1; i<=NF; i++) s=s+$i}; END{print s}'</pre>

 <p>Print every line after replacing each field with its absolute value
 <pre>
 awk '{for (i=1; i<=NF; i++) if ($i < 0) $i = -$i; print }'
 awk '{for (i=1; i<=NF; i++) $i = ($i < 0) ? -$i : $i; print }'
</pre>
<p> Print the total number of fields ("words") in all lines
<pre> awk '{ total = total + NF }; END {print total}' file</pre>

<p>Print the total number of lines that contain "Beth"
<pre> awk '/Beth/{n++}; END {print n+0}' file</pre>

<p>Print the largest first field and the line that contains it
  Intended for finding the longest string in field #1
 <pre>awk '$1 > max {max=$1; maxline=$0}; END{ print max, maxline}'</pre>

 <p>Print the number of fields in each line, followed by the line
 <pre>awk '{ print NF ":" $0 } '</pre>

 <p>Print the last field of each line
 <pre>awk '{ print $NF }'</pre>

 <p>Print the last field of the last line
 <pre>awk '{ field = $NF }; END{ print field }'</pre>

 <p> Print every line with more than 4 fields
 <pre>awk 'NF > 4'</pre>

 <p>Print every line where the value of the last field is > 4
 <pre>awk '$NF > 4'</pre>

<h2>Text Conversion and Substitution</h2>
 <p>IN UNIX ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format
 <pre>awk '{sub(/\r$/,"");print}'   # assumes EACH line ends with Ctrl-M</pre>

<p>IN UNIX ENVIRONMENT: convert Unix newlines (LF) to DOS format
<pre>awk '{sub(/$/,"\r");print}</pre>

 <p>IN DOS ENVIRONMENT: convert Unix newlines (LF) to DOS format
 <pre>awk 1</pre>

 <p>IN DOS ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format
 Cannot be done with DOS versions of awk, other than gawk:
 <pre>gawk -v BINMODE="w" '1' infile >outfile</pre>

 <p>Use "tr" instead.
<pre> tr -d \r <infile >outfile # GNU tr version 1.22 or higher</pre>

 <p> Delete leading whitespace (spaces, tabs) from front of each line
 aligns all text flush left
 <pre>awk '{sub(/^[ \t]+/, ""); print}'</pre>

 <p>Delete trailing whitespace (spaces, tabs) from end of each line
 <pre>awk '{sub(/[ \t]+$/, "");print}'
</pre>
 <p> Delete BOTH leading and trailing whitespace from each line
 <pre>awk '{gsub(/^[ \t]+|[ \t]+$/,"");print}'
awk '{$1=$1;print}'           # also removes extra space between fields
</pre>
 <p> Insert 5 blank spaces at beginning of each line (make page offset)
 <pre>awk '{sub(/^/, "     ");print}'
</pre>
 <p> Align all text flush right on a 79-column width
 <pre>awk '{printf "%79s\n", $0}' file*
</pre>
 <p> Center all text on a 79-character width
 <pre>awk '{l=length();s=int((79-l)/2); printf "%"(s+l)"s\n",$0}' file*
</pre>
 <p> Substitute (find and replace) "foo" with "bar" on each line
 <pre>awk '{sub(/foo/,"bar");print}'           # replaces only 1st instance
gawk '{$0=gensub(/foo/,"bar",4);print}'  # replaces only 4th instance
awk '{gsub(/foo/,"bar");print}'          # replaces ALL instances in a line
</pre>
 <p> Substitute "foo" with "bar" ONLY for lines which contain "baz"
 <pre>awk '/baz/{gsub(/foo/, "bar")};{print}'
</pre>
 <p> Substitute "foo" with "bar" EXCEPT for lines which contain "baz"
 <pre>awk '!/baz/{gsub(/foo/, "bar")};{print}'
</pre>
 <p> Change "scarlet" or "ruby" or "puce" to "red"
 <pre>awk '{gsub(/scarlet|ruby|puce/, "red"); print}'
</pre>
 <p> Reverse order of lines (emulates "tac")
 <pre>awk '{a[i++]=$0} END {for (j=i-1; j>=0;) print a[j--] }' file*
</pre>
 <p> If a line ends with a backslash, append the next line to it
  (fails if there are multiple lines ending with backslash...)
 <pre>awk '/\\$/ {sub(/\\$/,""); getline t; print $0 t; next}; 1' file*
</pre>
 <p> Print and sort the login names of all users
 <pre>awk -F ":" '{ print $1 | "sort" }' /etc/passwd
</pre>
 <p> Print the first 2 fields, in opposite order, of every line
 <pre>awk '{print $2, $1}' file
</pre>
 <p> Switch the first 2 fields of every line
 <pre>awk '{temp = $1; $1 = $2; $2 = temp}' file
</pre>
 <p> Print every line, deleting the second field of that line
 <pre>awk '{ $2 = ""; print }'
</pre>
 <p> Print in reverse order the fields of every line
 <pre>awk '{for (i=NF; i>0; i--) printf("%s ",i);printf ("\n")}' file
</pre>
 <p> Remove duplicate, consecutive lines (emulates "uniq")
 <pre>awk 'a !~ $0; {a=$0}'
</pre>
 <p> Remove duplicate, nonconsecutive lines
 <pre>awk '! a[$0]++'                     # most concise script
awk '!($0 in a) {a[$0];print}'      # most efficient script
</pre>
 <p> Concatenate every 5 lines of input, using a comma separator
  between fields
 <pre>awk 'ORS=%NR%5?",":"\n"' file
</pre>


<h2>Selective Printing of Certain Lines</h2>

 <p> Print first 10 lines of file (emulates behavior of "head")
 <pre>awk 'NR < 11'
</pre>
 <p> Print first line of file (emulates "head -1")
 <pre>awk 'NR>1{exit};1'
</pre>
  <p>Print the last 2 lines of a file (emulates "tail -2")
 <pre>awk '{y=x "\n" $0; x=$0};END{print y}'
</pre>
 <p> Print the last line of a file (emulates "tail -1")
 <pre>awk 'END{print}'
</pre>
 <p> Print only lines which match regular expression (emulates "grep")
 <pre>awk '/regex/'
</pre>
 <p> Print only lines which do NOT match regex (emulates "grep -v")
 <pre>awk '!/regex/'
</pre>
 <p> Print the line immediately before a regex, but not the line
 containing the regex
 <pre>awk '/regex/{print x};{x=$0}'
 awk '/regex/{print (x=="" ? "match on line 1" : x)};{x=$0}'
</pre>
 <p> Print the line immediately after a regex, but not the line
  containing the regex
 <pre>awk '/regex/{getline;print}'
</pre>
 <p> Grep for AAA and BBB and CCC (in any order)
 <pre>awk '/AAA/; /BBB/; /CCC/'
</pre>
 <p> Grep for AAA and BBB and CCC (in that order)
 <pre>awk '/AAA.*BBB.*CCC/'
</pre>
 <p> Print only lines of 65 characters or longer
 <pre>awk 'length > 64'
</pre>
 <p> Print only lines of less than 65 characters
 <pre>awk 'length < 64'
</pre>
 <p> Print section of file from regular expression to end of file
 <pre>awk '/regex/,0'
awk '/regex/,EOF'
</pre>
 <p> Print section of file based on line numbers (lines 8-12, inclusive)
 <pre>awk 'NR==8,NR==12'
</pre>
 <p> Print line number 52
 <pre>awk 'NR==52'
awk 'NR==52 {print;exit}'          # more efficient on large files
</pre>
 <p> Print section of file between two regular expressions (inclusive)
 <pre>awk '/Iowa/,/Montana/'             # case sensitive
</pre>

<h2>Selective Deletion of Certain Lines:</h2>

 <p> Delete ALL blank lines from a file (same as "grep '.' ")
 <pre>awk NF
awk '/./'
</pre>

<h2>Credits and Thanks</h2>
<p>
Special thanks to Peter S. Tillier for helping me with the first release
of this FAQ file.
<p>
For additional syntax instructions, including the way to apply editing
commands from a disk file instead of the command line, consult:
<ul>
<li>
"sed & awk, 2nd Edition," by Dale Dougherty and Arnold Robbins
  O'Reilly, 1997
<li>
"UNIX Text Processing," by Dale Dougherty and Tim O'Reilly
  Hayden Books, 1987
<li>
"Effective awk Programming, 3rd Edition." by Arnold Robbins
  O'Reilly, 2001
</ul>
<p>
To fully exploit the power of awk, one must understand "regular
expressions." For detailed discussion of regular expressions, see
<ul>
<li>"Mastering Regular Expressions, 2d edition" by Jeffrey Friedl
   (O'Reilly, 2002).
</ul>
<p>
The manual ("man") pages on Unix systems may be helpful (try "man awk",
"man nawk", "man regexp", or the section on regular expressions in "man
ed"), but man pages are notoriously difficult. They are not written to
teach awk use or regexps to first-time users, but as a reference text
for those already acquainted with these tools.
<p>
USE OF '\t' IN awk SCRIPTS: For clarity in documentation, we have used
the expression '\t' to indicate a tab character (0x09) in the scripts.
All versions of awk, even the UNIX System 7 version should recognize
the '\t' abbreviation.

