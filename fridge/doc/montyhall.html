<h1><join>The Monty Hall Problem</join></h1>
<p><a href="mailto:paddy3118@googlemail.com">Donald 'Paddy' McCarty</a>
has a nice Awk solution to the Monty Hall Problem, which he describes as follow:
<ul>
    <li> The contestant in in front of three  doors that he cannot see behind.. 
    <li> The three doors conceal one prize and the rest being booby prizes, arranged randomly.
    <li> The Host asks the contestant to choose a door.
    <li> The host then goes behind the doors where only he can see what is concealed, then always opens one door, out of the other s not chosen  by the contestant, that must reveal a booby prize to the contestant.
    <li> The host then asks the contestant if he would like either to stick with his previous choice, or switch and choose the other remaining closed door.
</ul>
<p>
It turns out that if the contestant follows a strategy of always switching when asked, then he will maximise his chances of winning.
Donald's simulator shows that:
<ul>
<li>
     A strategy of never switching wins 1/3rd of the time.
    <li> A strategy of randomly switching wins 1/2 of the time.
    <li> A strategy of always switching wins 2/3rds of the time.
</ul>
<h2>Code</h2>
<pre>
BEGIN {
	srand()
	doors = 3
	iterations = 10000
	# Behind a door: 
	EMPTY = "empty"; PRIZE = "prize"
	# Algorithm used
    KEEP = "keep"; SWITCH="switch"; RAND="random"; 
}
function monty_hall( choice, algorithm ) { # Set up doors
  for ( i=0; i&lt;doors; i++ ) {
		door[i] = EMPTY
	}
	door[int(rand()*doors)] = PRIZE # One door with prize

  chosen = door[choice]
  del door[choice]

  #if you didn't choose the prize first time around then
  # that will be the alternative
	alternative = (chosen == PRIZE) ? EMPTY : PRIZE 

	if( algorithm == KEEP) {
		return chosen
	} 
	if( algorithm == SWITCH) {
		return alternative
	} 
	return rand() &lt;0.5 ? chosen : alternative
}
function simulate(algo){
	prizecount = 0
	for(j=0; j&lt; iterations; j++){
		if( monty_hall( int(rand()*doors), algo) == PRIZE) { 
			prizecount ++ 
		}
	}
	printf "  Algorithm %7s: prize count = %i, = %6.2f%%\n", \
		algo, prizecount,prizecount*100/iterations
}
BEGIN {
	print "\nMonty Hall problem simulation:"
	print doors, "doors,", iterations, "iterations.\n"
	simulate(KEEP)
	simulate(SWITCH)
	simulate(RAND)
}
</pre>
<h2>Sample Output</h2>
<pre>
gawk -f montyHall.awk

Monty Hall problem simulation:
3 doors, 10000 iterations.

  Algorithm    keep: prize count = 3411, =  34.11%
  Algorithm  switch: prize count = 6655, =  66.55%
  Algorithm  random: prize count = 4991, =  49.91%
</pre>
